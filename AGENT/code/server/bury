#!/data/Software/mydan/perl/bin/perl -I/data/Software/mydan/AGENT/lib -I/data/Software/mydan/AGENT/private/lib
use strict;
use warnings;
use FindBin qw( $RealBin );
use POSIX;
use Util;
use Logs;

$| ++;

=head1 SYNOPSIS

    db => $mysql,

=cut

return sub
{
    my %param = @_;

    my ( $db, $logs ) = ( $param{db}, Logs->new( 'bury' ) );

    my $myname = `c3mc-base-hostname`;
    chomp $myname;

    while(1)
    {
        print "do ...\n";

        my $x = eval{ $db->query( "select id,pid,uuid,starttime,starttimems from openc3_agent_install 
            where slave='$myname' and pid is not null and status<>'success' and status<>'fail'" );};
        $logs->die( "mysql query fail: $@" ) if $@;
        $logs->die( "get list fail from mysql" ) unless defined $x && ref $x eq 'ARRAY';

        for my $r ( @$x )
        {
            my ( $id, $pid, $uuid, $starttime, $starttimems ) = @$r;
            next unless $pid =~ /^\d+$/;
            next unless $starttimems =~ /^[\.\d]+$/;
            next unless $uuid =~ /^[a-zA-Z0-9]+$/;

            next if kill( 0, $pid );

            my $time = time;
            $time = $starttimems + 1 if $time < $starttimems;

            my ( $runtime, $finishtime, $finishtimems ) = ( sprintf( "%0.3f", $time - $starttimems ), POSIX::strftime( "%Y-%m-%d %H:%M:%S", localtime( int $time ) ), $time );

            eval{ $db->execute( "update openc3_agent_install set status='fail',runtime='$runtime',
                finishtime='$finishtime',finishtimems='$finishtimems' where id=$id" );};
            $logs->err( "update install status fail: id=$id :$@" ) if $@;
        }
        sleep 3;
    }
}
