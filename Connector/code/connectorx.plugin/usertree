#!/data/Software/mydan/perl/bin/perl -I/data/Software/mydan/Connector/lib -I/data/Software/mydan/Connector/private/lib
use strict;
use warnings;
use Logs;
use LWP::UserAgent;
use Digest::MD5;
use JSON qw();
use Code;

$| ++;

=head1 SYNOPSIS

    cookie => 'cookiestring',
    logs => 日志对象

=cut

my ( $usertree, %env, $debug, $apicache, $treemapcode );
BEGIN{
    use Configini;
    $usertree = Configini::get( 'usertree' );
    %env = Configini::env( 'usertreeenv' );

    $debug = Configini::get( 'debug' );
    $apicache = Configini::get( 'apicache' );
    $apicache = 0 unless $apicache && $apicache =~ /^\d+$/;

    $treemapcode = Code->new( "connectorx.plugin/treemap" );
};

sub formatusertree
{
    my %map = @_;
    return [] unless %map;
    my ( @res, @currid, %submap );
    for my $id ( keys %map )
    {
        my @names = split /\./, $map{$id};
        if( @names == 1 )
        {
            push @currid, $id;
        }
        else
        {
            my ( $head, @name ) = @names;
            $submap{$head}{$id} = join '.', @name;
        }
    }

    for my $id ( sort{ $map{$a} <=> $map{$b} }@currid )
    {
        my $name = $map{$id};
        if( $submap{$name} )
        {
            push @res, +{ id => $id, name => $name, children => formatusertree( %{$submap{$name}}) };
        }
        else
        {
            push @res, +{ id => $id, name => $name };
        }
    }

    return \@res;
};


sub map2tree
{
    my $map = shift @_;
    my %map;
    map{ $map{$_->{id}} = $_->{name} }@$map;
    return formatusertree( %map );

};

return sub
{
    my ( %param )= @_;
    my ( $cookie, $logs ) = @param{qw(cookie logs)};

    unless( $usertree )
    {
        my $treemap = eval{ $treemapcode->run( cookie => $cookie ) };
        return map2tree( $treemap );
    }
    $logs = Logs->new( 'code.usertree' ) unless $logs;

    my $ua = LWP::UserAgent->new;
    $ua->default_header( %env ) if %env;
    $ua->timeout( 10 );

    my $file;
    if( $apicache )
    {
        my $md5 = Digest::MD5->new->add( "$usertree?cookie=$cookie" )->hexdigest;
        $file = "/tmp/openc3-cache-api-usertree-$md5";
        $ua->timeout( 3 );

        if( -f $file )
        {
            my $apicachetimeout = -f '/var/openc3_connector_api_fail' ? 86400 : $apicache;

            my $mtime = ( stat $file )[9];
            if( $mtime  && ( $mtime + $apicachetimeout > time ) )
            {
                my $filecont = eval{ YAML::XS::LoadFile $file };
                if( $filecont && ! $@ )
                {
                    $logs->say( "usertree api usetime: cache" );
                    return $filecont;
                }
            }
         }
    }



    my $t = time;
    my $res = $ua->get( "$usertree?cookie=$cookie" );

    $logs->say( YAML::XS::Dump $res ) if $debug;
    my $utime = time - $t;
    $logs->say( "usertree api usetime: $utime" );# if $debug;

    if($res->is_success)
    {
        my $v = eval{JSON::decode_json $res->decoded_content};
        eval{ YAML::XS::DumpFile $file, $v->{data} } if $apicache && $v->{stat};
        return $v->{data} if $v->{stat};
    }
    elsif( $apicache )
    {
        if( -f $file )
        {
            my $mtime = ( stat $file )[9];
            if( $mtime  && ( $mtime + 86400 > time ) )
            {
                my $filecont = eval{ YAML::XS::LoadFile $file };
                if( $filecont && ! $@ )
                {
                    $logs->err( "usertree api usetime: cache" );
                    return $filecont;
                }
            }
         }
    }
    die $res->content;
}
